<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SABatchTracingMenuItemManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Redirects calls to <c>SysFindTrace</c> form, to the <c>SABatchTracingFindTrace</c> form.
/// </summary>
internal class SABatchTracingMenuItemManager
{        
    private FormRun formRun;
        
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    [Hookable(false)]
    public void run()
    {
        if (formRun.name() != formStr(SysFindTrace))
        {
            throw error("@SYS104799");
        }
        
        if (SABatchTracingParameters::findOrCreate().SysFindTraceAdvanced)
        {
            MenuFunction menuFunction = this.newMenuFunction();
            if (menuFunction)
            {
                menuFunction.run(this.newArgsFromArgs(formRun.args()));
                formRun.lifecycleHelper().exitForm();
            }

        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>newMenuFunction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a <c>MenuFunction</c> class object for SABatchTracingFindTrace 
    /// </summary>
    /// <returns>
    /// A <c>MenuFunction</c> class object.
    ///</returns>
    [Hookable(false)]
    private MenuFunction newMenuFunction()
    {
        MenuFunction menuFunction;
            
        menuFunction = new MenuFunction(menuItemDisplayStr(SABatchTracingFindTrace), MenuItemType::Display);
        menuFunction.objectType(MenuItemObjectType::Form);
        menuFunction.object(formStr(SABatchTracingFindTrace));
        menuFunction.formViewOption(FormViewOption::Grid);
        
        return menuFunction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected static SABatchTracingMenuItemManager construct()
    {
        return new SABatchTracingMenuItemManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFormRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an object of the <c>SABatchTracingMenuItemManager</c> class.
    /// </summary>
    /// <param name = "_formRun">
    /// A <c>FormRun</c> class object.
    /// </param>
    /// <returns>
    /// A <c>SABatchTracingMenuItemManager</c> class object.
    /// </returns>
    [Hookable(false)]
    public static SABatchTracingMenuItemManager newFormRun(FormRun _formRun)
    {
        SABatchTracingMenuItemManager saBatchTracingMenuItemManager = SABatchTracingMenuItemManager::construct();
        saBatchTracingMenuItemManager.formRun = _formRun;
        return saBatchTracingMenuItemManager;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newArgsFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates and initiates an new <c>Args</c> class object from an existing <c>Args</c> class object.
    /// </summary>
    /// <param name = "_args">
    /// An <c>Args</c> class object.
    /// </param>
    /// <returns>
    /// An <c>Args</c> class object.
    ///</returns>
    [Hookable(false)]
    internal Args newArgsFromArgs(Args _args)
    {
        Args args = new Args();
        args.parmEnumType(_args.parmEnumType());
        args.record(_args.record());

        return args;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>